;; Shivgoraksh


[Collection: SEND_AccountOpening]
		
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.voucher'

	RemoteRequest: SEND_AccountOpening : UTF8
	JSON Object Path: "message:1"
	
;[#Menu: ExpressIntegration]
;	
;;	Add: Item : Send Contra : Call: SEND_Contra
;	Add: Item: Send Contra Cont : Call: SEND_Contra_Start

	[Function: SEND_AccountOpening_Start]
		
		10 : Start Timer: SEND_AccountOpening_Timer : 1
		
	[System: Events]
		
		SEND_AccountOpening_Timer : Timer: True: Call: SEND_AccountOpening

[Function: SEND_AccountOpening]

	Variable : vName  : String
	Variable: vMessage : String
	Variable: vDocName : String
	Variable: vStatus : Logical

	06 : Do If: ($$NumItems:SEND_AccountOpening_Tally = 0) :  Stop Timer: SEND_AccountOpening_Timer
	07 : Do If: ($$NumItems:SEND_AccountOpening_Tally = 0) :  Return

	08 : Stop Timer: SEND_AccountOpening_Timer
	09 : Start Progress: 1 : "Sending Account Opening Journal " : ""

	10 : Walk Collection: SEND_AccountOpening
	11 : Set: vStatus: $Status
	20 : 	If: $Status
	21 :	Start Batch Post
	25 : 		Walk Collection: data
	26 : 		Log: $name
	28 : 		Set: vName : $name
	28a:		Set: vDocName : $docname
	29 : 		Set: vMessage : $message
	30 : 			Modify Object: (Ledger, ##vName).ACC_Status[1].ACC_Status : @@ERPNextResetFlag
	32 : 			Modify Object: (Ledger, ##vName).ACC_Message[1].ACC_Message : ##vMessage
;	33 : 			Modify Object: (Voucher, ##vName).WebStatus_DocName[1].WebStatus_DocName : ##vDocName
	40 :		End Walk
	42 :	End Batch Post
	45 : 	End If
	60 : End Walk
	
	65 : Do If: ($$NumItems:SEND_AccountOpening_Tally > 0 and Not $$IsEmpty:##vStatus) :  Start Timer: SEND_AccountOpening_Timer : 1


;[#Menu: Gateway of Tally]
;	
;	Add: Contra:TEst1 : Display: SEND_Contra

[Report : SEND_AccountOpening]
	
	Form: SEND_AccountOpening
	Variable: SVFromDate, SVToDate
	Set: SVFromDate : @@EnableERPNextClosingDate
	Set: SVToDate	: @@EnableERPNextClosingDate
	
	[Form: SEND_AccountOpening]
		
		Part: SEND_AccountOpening
		Delete: XMLTag

	[Part: SEND_AccountOpening]
		
		Line: SEND_AccountOpening
		Repeat: SEND_AccountOpening : SEND_AccountOpening_Tally
		Scroll: Vertical
		
		[Line: SEND_AccountOpening]
			
			Field: ACC_name, ACC_Closing, ACC_doctype, ACC_naming_series, ACC_voucher_type, ACC_is_opening, ACC_company, ACC_posting_date
			Field: ACC_user_remark
			Field: ACC_MasterId, ACC_webstatus_docname, ACC_tally_voucherno
			Empty: $$Line > @@RecordsLimit 
			JSONTag: "data"			
			Explode: ACC_ledgers

			[Field: ACC_name]
				
				Set as: $Name
				Invisible: Yes

			[Field: ACC_Closing]
				
				Set as: $ClosingBalance
				Invisible: Yes

			[Field: ACC_doctype]
				
				Set as: "Journal Entry"
				JSONTag: "doctype"

			[Field: ACC_naming_series]
				
				Set as: @@ERPNextNamingSeries
				JSONTag: "naming_series"

			[Field: ACC_voucher_type]
				
				Set as: "Opening Entry"
				JSONTag: "voucher_type"
				
			[Field: ACC_is_opening]
				
				Set as: "Yes"
				JSONTag: "is_opening"

			[Field: ACC_company]
				
				Set as: @@ERPNextBranch
				JSONTag: "company"

			[Field: ACC_posting_date]
				
				Set as: $$GetSqlDate:@@EnableERPNextClosingDate
				JSONTag: "posting_date"
				
			[Field: ACC_user_remark]
				
				Set as: "Opening Balance"
				JSONTag: "user_remark"

			[Field: ACC_MasterId]
				
				Set as: $Name
				JSONTag: "tally_masterid"
		
			[Field: ACC_webstatus_docname]
				
				Set as: if $$IsEmpty:$WebStatus_DocName then "NA" else $WebStatus_DocName
				JSONTag: "webstatus_docname"
				
			[Field: ACC_tally_voucherno]
				
				Set as: "ACC-OP-" + $$String:$MasterId
				JSONTag: "tally_voucherno"

	[Part: ACC_ledgers]
		
		Line: ACC_ledgers
		Repeat: ACC_ledgers : ACC_LedgerEntries
		Scroll: Vertical
				
		[Line: ACC_ledgers]

			Field: ACC_Account, ACC_account_currency, ACC_cost_center, ACC_exchange_rate
			Field: ACC_debit_in_account_currency, ACC_debit, ACC_credit_in_account_currency, ACC_credit
			Field: ACC_is_advance
			JSONTag: "accounts"
			
			[Field: ACC_Account]

				JSONTag: "account"
				Set as: $LedgerName +  " - " + @@EnableERPNextCmpAbbr
		

			[Field: ACC_account_currency]

				JSONTag: "account_currency"
				Set as: "INR"
				Invisible: Yes

			[Field: ACC_cost_center]
				
				JSONTag: "cost_center"
				Set as: "Main - " + @@EnableERPNextCmpAbbr

			[Field: ACC_exchange_rate]

				JSONTag: "exchange_rate"
				Set as: "1"
				Invisible: Yes

			[Field: ACC_debit_in_account_currency]

				Type: Number
				JSONTag: "debit_in_account_currency"
				Set as: $debit_in_account_currency

			[Field: ACC_debit]

				Type: Number
				JSONTag: "debit"
				Set as: $debit

			[Field: ACC_credit_in_account_currency]

				JSONTag: "credit_in_account_currency"
				Set as: $credit_in_account_currency

			[Field: ACC_credit]

				JSONTag: "credit"
				Set as: $credit

			[Field: ACC_is_advance]

				JSONTag: "is_advance"
				Set as: "No"

						


[Collection: SEND_AccountOpening_Tally]

	Parm Var: SVFromDate : Date: @@EnableERPNextClosingDate
	Parm Var: SVToDate	 : Date: @@EnableERPNextClosingDate

	Type: Ledger
	Belongs To: Yes
	Filter : ACC_WebStatus
	Fetch: Name, Parent

	Filter: ACC_NonDebtors, ACC_NonCreditors, ACC_NonEmpty
	Filter: ACC_NonDirectIncomes, ACC_NonDirectExpenses, ACC_NonInDirectIncomes, ACC_NonInDirectExpenses, ACC_NonBank



		[Collection: ACC_LedgerEntries]
			
			Object: ACC_LedgerEntries_Debit, ACC_LedgerEntries_Credit
			
		[Object : ACC_LedgerEntries_Debit]
			
			LedgerName 					: if $$IsDr:#ACC_Closing then #ACC_name else "Temporary Opening"
			debit						: if $$IsDr:#ACC_Closing then $$Abs:$$Number:#ACC_Closing else 0
			debit_in_account_currency	: if $$IsDr:#ACC_Closing then $$Abs:$$Number:#ACC_Closing else 0

			credit						: if $$IsDr:#ACC_Closing then 0 else $$Abs:$$Number:#ACC_Closing
			credit_in_account_currency	: if $$IsDr:#ACC_Closing then 0 else $$Abs:$$Number:#ACC_Closing

		[Object : ACC_LedgerEntries_Credit]
			
			LedgerName 					: if $$IsDr:#ACC_Closing then "Temporary Opening" else #ACC_name

			debit						: if $$IsDr:#ACC_Closing then 0 else $$Abs:$$Number:#ACC_Closing
			debit_in_account_currency	: if $$IsDr:#ACC_Closing then 0 else $$Abs:$$Number:#ACC_Closing

			credit						: if $$IsDr:#ACC_Closing then $$Abs:$$Number:#ACC_Closing else 0
			credit_in_account_currency	: if $$IsDr:#ACC_Closing then $$Abs:$$Number:#ACC_Closing else 0
	

[System: Formula]
	
	ACC_WebStatus 		: $ACC_Status != @@ERPNextResetFlag

	ACC_NonDebtors 	 	: Not $$IsLedOfGrp:$Name:$$GroupSundryDebtors
	ACC_NonCreditors 	: Not $$IsLedOfGrp:$Name:$$GroupSundryCreditors
	ACC_NonDirectIncomes	 	: Not $$IsLedOfGrp:$Name:$$GroupDirectIncomes
	ACC_NonDirectExpenses	 	: Not $$IsLedOfGrp:$Name:$$GroupDirectExpenses
	ACC_NonInDirectIncomes	 	: Not $$IsLedOfGrp:$Name:$$GroupIndirectIncomes
	ACC_NonInDirectExpenses	 	: Not $$IsLedOfGrp:$Name:$$GroupIndirectExpenses
	ACC_NonBank		 	: Not $$IsLedOfGrp:$Name:$$GroupBank
	ACC_NonEmpty 		: Not $$IsEmpty:$ClosingBalance

	[System: UDf]
		
		ACC_Status:  String: 31579
		ACC_Message : String: 31580

