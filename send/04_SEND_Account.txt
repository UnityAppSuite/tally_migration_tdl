;; Shivgoraksh
;; Batched Account Send - Max 10 records per batch


[Collection: SEND_Account]

	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.account'

	RemoteRequest: SEND_Account : UTF8
	JSON Object Path: "message:1"

	[Function: SEND_Account_Start]

		Variable: vBatchSize : Number: 10
		Variable: vTotalSent : Number: 0
		Variable: vTotalRecords : Number: $$NumItems:SEND_Account_Tally

		10 : Log: "Starting batch send. Total accounts: " + $$String:##vTotalRecords
		20 : Start Timer: SEND_Account_Timer : 1

	[System: Events]

		SEND_Account_Timer : Timer: True: Call: SEND_Account


[Function: SEND_Account]

	Variable : vName  : String
	Variable : vMessage : String
	Variable : vStatus : String
	Variable : vCurrentBatchCount : Number: 0
	Variable : vRecordCounter : Number: 0

	06 : Do If: ($$NumItems:SEND_Account_Tally = 0) :  Stop Timer: SEND_Account_Timer
	07 : Do If: ($$NumItems:SEND_Account_Tally = 0) :  Log: "All accounts sent. Total: " + $$String:##vTotalSent
	08 : Do If: ($$NumItems:SEND_Account_Tally = 0) :  Return

	09 : Stop Timer: SEND_Account_Timer
	10 : Start Progress: $$NumItems:SEND_Account_Tally : "Sending Accounts (Batch of 10)" : "Processing..."

	11 : Walk Collection: SEND_Account

	12 : 	Set: vStatus : $Status
	13 : 	If: Not $$IsEmpty:##vStatus
	14 :		Start Batch Post
	15 : 		Walk Collection: data
	16 : 			Increment: vRecordCounter
	17 : 			If: ##vRecordCounter <= ##vBatchSize
	18 : 				Log: "[" + $$String:##vRecordCounter + "/" + $$String:##vBatchSize + "] " + $name
	19 : 				Set: vName : $name
	20 : 				Set: vMessage : $message
	21 : 				Modify Object: (Ledger, ##vName).WebStatus[1].WebStatus : @@ERPNextResetFlag
	22 :				Modify Object: (Ledger, ##vName).WebStatus_message[1].WebStatus_message : ##vMessage
	23 :				Increment: vCurrentBatchCount
	24 :				Increment: vTotalSent
	25 :				Show Progress: ##vTotalSent
	26 : 			End If
	27 :		End Walk
	28 : 		End Batch Post
	29 : 	End If
	30 : End Walk

	31 : Log: "Batch complete. Sent " + $$String:##vCurrentBatchCount + " accounts. Total sent: " + $$String:##vTotalSent
	32 : Do If: ($$NumItems:SEND_Account_Tally > 0) :  Start Timer: SEND_Account_Timer : 2
	

;[#Menu: Gateway of Tally]
;	
;	Add: Item: Request - Customer: Display: SEND_Customer

[Report : SEND_Account]
	
	Form: SEND_Account
	
	[Form: SEND_Account]
		
		Part: SEND_Account
		Delete: XMLTag

	[Part: SEND_Account]
		
		Line: SEND_Account
		Repeat: SEND_Account : SEND_Account_Tally
		Scroll: Vertical
		
		[Line: SEND_Account]
			
			Field: SEND_Account_name, SEND_Account_doctype, SEND_Account_group
			Field: SEND_company

			JSONTag: "data"
			Empty: $$Line > @@RecordsLimit
						
			[Field: SEND_Account_name]
				
				Set as: $Name
				JSONTag: "account_name"
				
				
			[Field: SEND_Account_doctype]
				
				Set as: "Account"
				JSONTag: "doctype"

			[Field: SEND_Account_group]

				Set as: $Parent + " - " + @@EnableERPNextCmpAbbr
				JSONTag : "parent_account"

[Collection: SEND_Account_Tally]
	
	Type: Ledger
	Belongs To: Yes
	Filter : WebStatus
	Fetch: Name, Parent
	Filter: NonDebtors, NonCreditors
	
	[System: Formula]
		
		NonDebtors : Not $$IsGroupSundryDebtors:$Parent
		NonCreditors : Not $$IsGroupSundryCreditors:$Parent

;	Filter: Customer_DebugFilter
;	
;	[System: Formula]
;		
;		Customer_DebugFilter: $Name IN ("2N 2N Enterprises, Hata Bharandi", "3 Star Electrical, Umarkot")
